{
Copyright (C) Alexey Torgashin, uvviewsoft.com
License: MPL 2.0 or LGPL
}
unit ATSynEdit_Export_HTML;

{$mode objfpc}{$H+}

interface

uses
  Classes, SysUtils, Graphics, StrUtils,
  ATSynEdit,
  ATSynEdit_CanvasProc,
  ATStringProc_HtmlColor;

procedure DoEditorExportToHTML(Ed: TATSynEdit;
  const AFilename, APageTitle, AFontName: string;
  AFontSize: integer; AWithNumbers: boolean;
  AColorBg, AColorNumbers: TColor);

implementation


procedure DoCssStyle(AColorFont, AColorBg, AColorFontDef, AColorBgDef: TColor;
  out StyleName, StyleText: string);
var
  NameF, TextF, NameBg, TextBg: string;
begin
  if AColorFont<>AColorFontDef then
  begin
    NameF:= SColorToHtmlColor(AColorFont);
    TextF:= 'color: '+NameF+'; ';
    Delete(NameF, 1, 1);
  end
  else
  begin
    NameF:= '';
    TextF:= '';
  end;

  if AColorBg<>AColorBgDef then
  begin
    NameBg:= SColorToHtmlColor(AColorBg);
    TextBg:= 'background: '+NameBg+'; ';
    Delete(NameBg, 1, 1);
  end
  else
  begin
    NameBg:= '';
    TextBg:= '';
  end;

  StyleName:= '_'+NameF;
  if NameBg<>'' then
    StyleName+= '_'+NameBg;
  StyleText:= '    .'+StyleName+' {'+TextF+TextBg+'}';
end;


function _IsSpaces(const S: string): boolean;
var
  i: integer;
begin
  for i:= 1 to Length(S) do
    if S[i]<>' ' then exit(false);
  Result:= true;
end;

procedure DoEditorExportToHTML(Ed: TATSynEdit; const AFilename, APageTitle,
  AFontName: string; AFontSize: integer; AWithNumbers: boolean; AColorBg,
  AColorNumbers: TColor);
const
  cPlaceStyles = '<<c>>';
var
  L, LStyles: TStringList;
  Parts: TATLineParts;
  PPart: ^TATLinePart;
  NColorFont: TColor;
  NColorAfter: TColor;
  NeedStyle: boolean;
  Str0, StrText: string;
  StyleName, StyleText: string;
  i, j: integer;
begin
  NColorFont:= clBlack;
  FillChar(Parts, Sizeof(Parts), 0);

  if FileExists(AFilename) then
    DeleteFile(AFilename);

  L:= TStringList.Create;
  LStyles:= TStringList.Create;
  try
    L.Add('<!-- Generated by ATSynEdit Exporter -->');
    L.Add('<html>'+sLineBreak+
               '<head>'+sLineBreak+
               '  <meta http-equiv="Content-Type" content="text/html; charset=utf-8" />'+sLineBreak+
               '  <title>'+APageTitle+'</title>'+sLineBreak+
               '  <style>'+sLineBreak+
               '    body, table {'+sLineBreak+
               '      color: '+SColorToHtmlColor(NColorFont)+';'+sLineBreak+
               '      background-color: '+SColorToHtmlColor(AColorBg)+';'+sLineBreak+
               '    }'+sLineBreak+
               '    pre, code {'+sLineBreak+
               '      font-family: "'+AFontName+'", sans-serif;'+sLineBreak+
               '      font-size: '+IntToStr(AFontSize)+'px;'+sLineBreak+
               '    }'+sLineBreak+
               '    table, td {'+sLineBreak+
               '      border-style: hidden;'+sLineBreak+
               '    }'+sLineBreak+
               '    td {'+sLineBreak+
               '      vertical-align: top;'+sLineBreak+
               '    }'+sLineBreak+
               '    td.num {'+sLineBreak+
               '      color: '+SColorToHtmlColor(AColorNumbers)+';'+sLineBreak+
               '      text-align: right;'+sLineBreak+
               '    }');
      L.Add(cPlaceStyles);
      L.Add(
               '  </style>'+sLineBreak+
               '</head>'+sLineBreak+
               '<body>');

    if AWithNumbers then
    begin
      L.Add('<table>'+sLineBreak+'<tr>'+sLineBreak+'<td class="num">');
      for i:= 0 to Ed.Strings.Count-1 do
      begin
        Str0:= IntToStr(i+1)+'&nbsp;&nbsp;';
        if i=0 then
          Str0:= '<pre><code>'+Str0;
        L.Add(Str0);
      end;
      L.Add('</code></pre>');
      L.Add('</td>'+sLineBreak+'<td>');
    end;

    for i:= 0 to Ed.Strings.Count-1 do
    begin
      Str0:= '';
      if not Ed.DoCalcLineHiliteEx(i, Parts, AColorBG, NColorAfter) then break;
      for j:= 0 to High(Parts) do
      begin
        PPart:= @Parts[j];
        if PPart^.Len=0 then Break;

        StrText:= Ed.Strings.LineSub(i, PPart^.Offset+1, PPart^.Len);
        StrText:= StringReplace(StrText, '<', '&lt;', [rfReplaceAll]);
        StrText:= StringReplace(StrText, '>', '&gt;', [rfReplaceAll]);

        if _IsSpaces(StrText) then
        begin
          Str0+= StrText;
          Continue;
        end;

        if PPart^.FontBold then Str0+= '<b>';
        if PPart^.FontItalic then Str0+= '<i>';
        if PPart^.FontStrikeOut then Str0+= '<s>';

        NeedStyle:=
          (PPart^.ColorFont<>NColorFont) or
          (PPart^.ColorBG<>AColorBG);

        if NeedStyle then
        begin
          DoCssStyle(
            PPart^.ColorFont, PPart^.ColorBG,
            NColorFont, AColorBG,
            StyleName, StyleText
            );
          if LStyles.IndexOf(StyleText)<0 then
            LStyles.Add(StyleText);
          Str0+= Format('<span class="%s">', [StyleName]);
        end;

        Str0:= Str0+StrText;
        if NeedStyle then
          Str0+= '</span>';

        if PPart^.FontStrikeOut then Str0+= '</s>';
        if PPart^.FontItalic then Str0+= '</i>';
        if PPart^.FontBold then Str0+= '</b>';
      end;

      if i=0 then
        Str0:= '<pre><code>'+Str0;
      L.Add(Str0);
    end;

    L.Add('</code></pre>');
    if AWithNumbers then
      L.Add('</td></tr></table>');

    L.Add('</body>');
    L.Add('</html>');

    //find place for CSS styles and insert LStyles here
    i:= L.IndexOf(cPlaceStyles);
    if i>=0 then
    begin
      L.Delete(i);
      for j:= LStyles.Count-1 downto 0 do
        L.Insert(i, LStyles[j]);
    end;

    L.SaveToFile(AFilename);
  finally
    FreeAndNil(LStyles);
    FreeAndNil(L);
  end;
end;

end.

